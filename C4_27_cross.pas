{ Специальная камера, установленная на перекрёстке, 
фиксирует количество проезжающих автомобилей, 
и каждую минуту по каналу связи передаёт неотрицательное целое число — количество автомобилей, 
проехавших перекрёсток за эту минуту. 
Известно, что за минуту перекрёсток может проехать не более 100 автомобилей. 
Необходимо найти в заданной серии показаний максимальное количество автомобилей, 
проехавших перекрёсток в течение пяти подряд идущих минут. 
Максимальное количество показаний, которое может передать камера, не превышает 1440.
Напишите на любом языке программирования программу для решения поставленной задачи. 
Для получения максимального результата программа должна быть эффективна по времени и по используемой памяти 
}

var
  N : integer; // Количество показаний
  A : array [0..4] of integer; // Показания камеры (очередь)
  i, sum, max : integer; 
begin
  // Читаем из файла, а не с клавиатуры
  assign(input, 'C4_27_cross.txt');
  reset(input);
  // В первой строке задаётся число N – общее количество переданных показаний. 
  // Гарантируется, что N>5. 
  readln(N);
  // В каждой из следующих N строк задаётся одно 
  // положительное целое число – очередное показание камеры.
  // Читаем первые 5 элементов и считаем их сумму
  // Это начальное заполнение кольцевой очереди
  sum := 0; // Текущая сумма
  for i:=1 to 5 do begin
    readln(A[i mod 5]);
    sum := sum + A[i mod 5];
  end;
  max := sum; // Текущий максимум
  for i:=6 to N do begin
    sum := sum - A[i mod 5]; // Убираем элемент из суммы
    readln(A[i mod 5]); // Заменяем элемент на новый
    sum := sum + A[i mod 5]; // Меняем сумму
    if sum > max then // Если она больше масимума
      max := sum;     // => обновляем максимум
  end;
  writeln(max); // Выводим ответ (максимум)
end.